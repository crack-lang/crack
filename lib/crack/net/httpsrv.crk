## HTTP Server Framework.  Usage synopsis:
##   HTTPServer server = {
##
## Copyright 2012 Google Inc.
## Copyright 2012 Shannon Weyrick <weyrick@mozek.us>
## Copyright 2012 Conrad Steenberg <conrad.steenberg@gmail.com>
## 
##   This Source Code Form is subject to the terms of the Mozilla Public
##   License, v. 2.0. If a copy of the MPL was not distributed with this
##   file, You can obtain one at http://mozilla.org/MPL/2.0/.
## 

import crack.ascii parseInt, strip, toLower;
import crack.fieldset HashMapFieldSet;
import crack.lang AppendBuffer, Buffer, Exception, ManagedBuffer, WriteBuffer, Formatter;
import crack.strutil split, StringArray;
import crack.cont.array Array;
import crack.cont.hashmap HashMap;
import crack.io cerr, FStr, StandardFormatter, StringFormatter, StringWriter, 
    Writer, FileHandle;
import crack.net InetAddress, Poller, Socket, AF_INET, POLLIN,
    POLLOUT, POLLERR, SOCK_STREAM;
import crack.time TimeDelta;
import crack.sys strerror;
import crack.lang SystemError;
import crack.runtime errno;
import crack.functor Functor1, Functor2, Function1;
import crack.logger debug, info, error;

@import crack.ann define, interface, implements;

StandardFormatter NULL_FORMATTER = null;

## Post data handlers are used to allow request handlers to defer reading of 
## contents from a PUT/POST request.  The request handler must set this in the 
## HTTPRequest object otherwise this data will be discarded.
#alias PostDataHandler = Functor2[void, HTTPRequest, Buffer];

## The primary request handler should attach a ResponseHandler to the request 
## if it either needs to block or is returning something to large to 
## comfortably fit into memory.  The response handler should return true when 
## it is done sending data back to the client.
#alias ResponseHandler = Functor2[bool, HTTPRequest];

## Contains the full contents of an HTTP request and provides the means for 
## communicating back to the client.
## Note that this is a FieldSet, arbitrary user data can be attached to it.
class HTTPRequest : HashMapFieldSet {
    String method, path, version;
    HashMap[String, String] headers = {};
    InetAddress clientAddr;
    StandardFormatter __clt;
    
    ## If present, this is the path parsed into components, e.g. 
    ## with a path of "/foo/bar/baz" this would normally contain ["foo", 
    ## "bar", "baz"].  Use getParsedPath() if you want to create it from 
    ## 'path' when it's null.  Handlers may mutate this field for other 
    ## handlers on the path.  For example, Dispatchers typically remove the 
    ## first element (which they have already matched).
    StringArray parsedPath;

    ## If non-null, this is the contents of the request sent with a POST/PUT 
    ## request.  It is the responsibility of the handler to populate this.
    Buffer contents;

    Functor2[void, HTTPRequest, Buffer] postDataHandler;
    Functor1[bool, HTTPRequest] responseHandler;

    ## The number of bytes of POST/PUT data that hasn't been processed yet.
    ## This is updated after postDataHandler is called, so it includes the 
    ## data in the buffer passed to postDataHandler.
    uint remainingPostData;

    oper init(Writer client, ## the channel to write back to the client.
              InetAddress clientAddr ## the client address.
              ) :
        method = method,
        path = path,
        version = version,
        clientAddr = clientAddr,
        __clt(client) {
    }
    
    ## Sends a reply to the client with the specified code, content type and 
    ## contents.
    void sendReply(int code, String contentType, Buffer contents) {
        __clt `HTTP/1.1 $code Stat
Content-type: $contentType
Content-length: $(contents.size)

$contents`;
    }
    
    void sendRedirect(int code, String location) {
        __clt `HTTP/1.1 $code Stat
Location: $location

`;
    }

    ## Returns the parsed path (see 'parsedPath' above), creating it from 
    ## 'path' if the field is currently null.
    @final StringArray getParsedPath() {
        if (parsedPath is null)
            parsedPath = split(path.substr(1), '/');
        return parsedPath;
    }

    ## Writes the response status and some of the headers.  It is the 
    ## responsibility of the caller to write the "content-length" header and a 
    ## blank line prior to writing the message contents.
    void writePartialHeaders(int code, String contentType) {
        __clt I`HTTP/1.1 $code Stat
               Content-type: $contentType
               `;
    }
    
    ## Write all headers.  Caller is responsible for writing contents.
    void writeHeaders(int code, String contentType, int contentLength) {
        writePartialHeaders(code, contentType);
        __clt I`Content-length: $contentLength

                `;
    }
    
    void write(Buffer data) {
        __clt.write(data);
    }
}

uintz normalizeIndex(intz index, uintz size) {
    if (index < 0)
        index = size + index;
    
    # if it's still zero, trim to zero
    if (index < 0)
        index = 0;
    
    # if greater than the limit, trim to the limit.
    else if (index > size)
        index = size;
    
    return index;
}

## Joins an array of path components into a path.
String joinPath(StringArray path, int start, int end) {
    size := path.count();
    s := normalizeIndex(start, size);
    e := normalizeIndex(end, size);

    # figure out how much space we need for all strings separated by slashes.
    uintz bufSize;
    for (elem :in path)
        bufSize += elem.size;
    bufSize += size - 1;

    AppendBuffer buf = {bufSize};
    for (int i = start; i < end; ++i) {
        if (i == start)
            buf.append(b'/');
        buf.extend(path[i]);
    }
    
    return String(buf, true);
}
        

const int _ST_REQUEST = 0;
const int _ST_HEADERS= 1;
const int _ST_BODY = 2;

## Error formatter.  Writes a message to cerr (TODO: to a logger) and sends an 
## error reply with the message as its parcel.
class Error : StringFormatter {
    HTTPRequest req;
    int code;
    oper init(HTTPRequest req, int code) : req = req, code = code {}

    void leave() {
        result := createString();
        cerr.write(result);
        req.sendReply(code, "text/plain", result);
    }
}

## Request handler interface.  This interface dispatches a handler to 
## interface methods corresponding to the HTTP GET, PUT and POST methods.
@interface HTTPRequestHandler : Functor1[bool, HTTPRequest] {
    bool onGet(HTTPRequest req) {
        Error(req, 405) `GET Method not allowed for $(req.path)`;
        return true;
    }

    bool onPut(HTTPRequest req) {
        Error(req, 405) `PUT Method not allowed for $(req.path)`;
        return true;
    }
    
    bool onPost(HTTPRequest req) {
        Error(req, 405) `PUT Method not allowed for $(req.path)`;
        return true;
    }

    bool oper call(HTTPRequest req) {
        if (req.method == 'GET')
            return onGet(req);
        else if (req.method == 'POST')
            return onPost(req);
        else if (req.method == 'PUT')
            return onPut(req);
        else {
            Error(req, 405) `Invalid method $(req.method)`;
            return false;
        }
    }
        
}

alias RequestHandler = Functor1[bool, HTTPRequest];
alias HandlerFunc = Function1[bool, HTTPRequest];
alias HandlerArray = Array[RequestHandler];

class OutQueue : Array[String] @implements Writer {
    void write(Buffer data) {
        if (data.isa(String))
            append(String.cast(data));
        else
            append(String(data));
    }
    void write(byteptr data) {
        append(String(data));
    }
}

## An HTTP Server.
##
## TODO: make this class proactor based.
class HTTPServer {
    
    class Client {
        ManagedBuffer inbuf = {1024};
        HTTPServer server;
        HTTPRequest request;
        HandlerArray handlers;
        Socket sock;
        InetAddress addr;
        int state;
        
        OutQueue outQueue = {};
                
        ## the index of the first byte in the buffer that has been read but 
        ## not processed.
        uint pos;
        
        oper init(Socket sock, InetAddress addr, HandlerArray handlers) :
            sock = sock, 
            addr = addr,
            handlers = handlers {
        }

        void formatTo(Formatter fmt) {
            fmt `Client: $sock, $addr `;
        }

        ## Returns a byteptr to the current input buffer read position.
        @final byteptr getInputBuf() {
            if (inbuf.cap - inbuf.size < 1024)
                # since the start size is 1024, we can assume that we will 
                # grow to accomodate a 1024 byte buffer.
                inbuf.grow(inbuf.cap * 2);
            return inbuf.buffer + pos;
        }

        @final void compressBuffer(uintz start) {
            inbuf.compact(start);
            pos = 0;
        }

        @final void __badRequest() {
            cerr `Invalid request from $(this)\n`;
        }        
        
        @final void processRequestLine(uintz start, uintz end) {
            request = HTTPRequest(outQueue, addr);

            # find the first space
            methodEnd := inbuf.lfind(b' ', start);
            if (methodEnd == -1 || methodEnd >= end) {
                __badRequest();
                return;
            }
            request.method = String(inbuf.buffer + start, methodEnd, false);

            # find the next non-space 
            pathStart := methodEnd;
            while (inbuf.buffer[pathStart] == b' ' && pathStart < end) 
                ++pathStart;
            if (pathStart == end) {
                __badRequest();
                return;
            }

            # find the path end            
            pathEnd := inbuf.lfind(b' ', pathStart);
            if (pathEnd == -1 || pathEnd > end) {
                __badRequest();
                return
            }

            # find the protocol version            
            request.path = String(inbuf.buffer + pathStart, 
                                  pathEnd - pathStart,
                                  false
                                  );
            
            # deal with HTTP 1.0
            if (pathEnd == end) {
                state = _ST_REQUEST;
                return;
            }

            # find the next non-space
            verStart := pathEnd;
            while ((c := inbuf.buffer[verStart]) == b' ' && verStart < end)
                ++verStart;
            if (verStart == end) {
                __badRequest();
                return;
            }
            request.version = String(inbuf.buffer + verStart,
                                     end - verStart,
                                     false
                                     );
            info `got header $(request.version)\n`;
            state = _ST_HEADERS;
        }

        @final void processRequest() {
            for (handler :in handlers) {
                if (handler(request))
                    break;
            }
            
            if (request.method != 'POST' && request.method != 'PUT') {
                state = _ST_REQUEST;
                request = HTTPRequest(outQueue, addr);
            }
        }
        
        @final void processHeaderLine(uintz start, uintz end) {
            line := String(inbuf.buffer + start, end - start, false);
            info `processing header line: $(line.getRepr())\n`;
            if (!line) {
                state = _ST_BODY;
                if (request.method == 'POST' || request.method == 'PUT') {
                    # For requests with a body, get the content-length.
                    contentLength := request.headers.get('content-length', 
                                                         null
                                                         );
                    if (contentLength)
                        request.remainingPostData = parseInt(contentLength);
                    else
                        request.remainingPostData = 0xFFFFFFFF;

                    # Discard the request line and headers.  'inbuf' now 
                    # contains only the body.
                    compressBuffer(end + (inbuf.buffer[end] == b'\r' ? 2 : 1));
                }
                processRequest();
                return;
            }
            # find the first colon
            sep := line.lfind(b':');
            info `adding $(line.slice(0, sep)) $(line.slice(sep + 1))\n`;
            request.headers[toLower(line.slice(0, sep))] =
                strip(line.slice(sep + 1));
        }
                    
        @final void parseNext(uint amtRead) {
            end := inbuf.size + amtRead;
            inbuf.size += amtRead;
            while (true) {
                if (state == _ST_REQUEST || state == _ST_HEADERS) {
                    
                    # find the first newline
                    int p;
                    for (p = pos; inbuf.buffer[p] != b'\n' && p < end; ++p);
                    
                    # if we got one, process the request line
                    if (p < end) {
                        
                        start := pos;
                        pos = p + 1;

                        # ignore a carrriage-return
                        if (p > 1 && inbuf.buffer[p - 1] == b'\r')
                            --p;
                    
                        if (state == _ST_REQUEST)
                            processRequestLine(start, p);
                        else
                            processHeaderLine(start, p);

                    } else {
                        # XXX verify that we haven't yet exceeded the maximum 
                        # request line length
                        # no complete line yet
                        break;
                    }
                } else {
                    # State is _ST_BODY.  Deal with the "end of parcel" 
                    # condition correctly and if the handler has defined a post 
                    # data handler, call it.
                    if (request.remainingPostData >= inbuf.size) {
                        if (request.postDataHandler) {
                            debug `calling post data handler with inbuf [$inbuf]\n`;
                            request.postDataHandler(request, inbuf);
                        }
                        request.remainingPostData -= inbuf.size;
                        compressBuffer(inbuf.size);
                    } else {
                        # The inbuf contains more than the contents (it 
                        # overlaps the beginning of the next request)
                        if (request.postDataHandler)
                            request.postDataHandler(
                                request, 
                                Buffer(inbuf.buffer, request.remainingPostData)
                            );
                        compressBuffer(request.remainingPostData);
                        request.remainingPostData = 0;
                    }
                    
                    # Begin the new request if we're done with post data.
                    if (!request.remainingPostData) {
                        state = _ST_REQUEST;
                        request = HTTPRequest(outQueue, addr);
                    }
                    return;
                }
            }

            compressBuffer(pos);
        }

        @final bool readChunk() {
            uint amtRead;
            try {
                amtRead = sock.read(WriteBuffer(getInputBuf(), 0, 1024));
            } catch (Exception ex) {
                info `WARN: failed to read from socket: $ex\n`;
                return false;
            }
            if (!amtRead) {
                return false;
            } else {
                parseNext(amtRead);
                return true;
            }
        }
    } // end of Client
    
    int __port;
    Socket __sock = {AF_INET, SOCK_STREAM, 0};
    HashMap[FileHandle, Client] __clients = {};
    Poller __poller = {};
    Array[FileHandle] __toRemove = {};
    HandlerArray __handlers = {};

    oper init(int port) {
        if (!__sock.bind(InetAddress(0, port))) 
            throw SystemError('bind failed', errno());
        if (!__sock.listen(5)) 
            throw SystemError('listen failed', errno());
        if (!__sock.setReuseAddr(true))
            info `WARN: reuseaddr failed: $(strerror())\n`;
        __poller.add(__sock, POLLIN);
    }

    void __accept() {
        clientAccepted := __sock.accept();
        info `  got connection from $(clientAccepted.addr) with ref count \
$(clientAccepted.sock.refCount) on $(clientAccepted.sock.fd)\n`;
        __poller.add(clientAccepted.sock, POLLIN);
        __clients[clientAccepted.sock] =
            Client(clientAccepted.sock,
                   InetAddress.cast(clientAccepted.addr),
                   __handlers);
    }
    
    void __processClientRead(FileHandle p) {
        client := __clients[p];
        if (!client.readChunk()) {
            info `deleting client $p)\n`;
            __clients.delete(p);
            # XXX should do a deferred close here
            client.sock.close();
            __toRemove.append(p);
        }
    }
    
    void __processClientWrite(FileHandle p) {
        # find the client
        client := __clients[p];
        
        # XXX this is rubbish.  Fix it
        while (client.outQueue) {
            info `writing $(client.outQueue[0].getRepr())\n`;
            client.sock.send(client.outQueue[0], 0);
            client.outQueue.delete(0);
        }
    }

    ## Adds the given handler to the chain.  Handlers are called in the order 
    ## in which they were added until one of them returns true.
    void addHandler(Functor1[bool, HTTPRequest] handler) {
        __handlers.append(handler);
    }
    
    ## Process a single event.
    ## If 'timeout' is not null, it is the timeout to wait for the next event.
    void processOnce(TimeDelta timeout) {
        
        # go through the clients, change their events
        for (clientItem :in __clients)
            if (clientItem.val.outQueue)
                __poller.setEvents(clientItem.val.sock, POLLIN | POLLOUT);
            else
                __poller.setEvents(clientItem.val.sock, POLLIN);
        
        debug `waiting for an event\n`;
        result := __poller.wait(timeout);
        debug `got events: $result\n`;
        while (evt := __poller.nx()) {
            info `  event is on $(evt.pollable)\n`;
            # if it's the server socket, do an accept
            if (evt.pollable is __sock) {
                info `  got new connection\n`;
                __accept();
            # client events
            } else {
                if (POLLIN & evt.revents) {
                    info `  got client read\n`;
                    __processClientRead(evt.pollable);
                } else if (POLLOUT & evt.revents) {
                    info `  got client writable\n`;
                    __processClientWrite(evt.pollable);
                } else if (POLLERR & evt.revents) {
                    info `  error on socket $(evt.pollable)\n`;
                } else {
                    info `  wtf???  pollable = $(evt.pollable), events = \
$(evt.events), revents = $(evt.revents)\n`;
                }
            }
        }
        
        # remove pollables that we're done with.
        if (__toRemove) {
            for (p :in __toRemove) {
                info `removing $p\n`;
                __poller.remove(p);
            }
            while (__toRemove)
                __toRemove.delete(0);
            info `removals finished\n`;
        }
        
    }
    
    void run() {
        while (true) {
            processOnce(null);
        }
    }
    
    int getPort() { return __port; }
}

## A Chain is a sequence of handlers. It replicates the logic of the 
## HTTPServer itself, calling all of its handlers until one of them returns 
## 'true' at which point it returns true, also.  If no handler returns true, 
## this functor returns false.
class Chain : Array[RequestHandler] @implements Functor1[bool, HTTPRequest] {
    bool oper call(HTTPRequest req) {
        for (handler :in this) {
            if (handler(req))
                return true;
        }
        return false;
    }
}

## A PathDispatcher is a map of handlers that calls and returns the return 
## value of the handler mapped to the first segment of the URL path, if there 
## is one.  Returns 'false' if there is no match.
class PathDispatcher : HashMap[String, RequestHandler] 
                       @implements Functor1[bool, HTTPRequest] {
    bool oper call(HTTPRequest req) {
        handler := get(req.getParsedPath()[0], null);
        if (!(handler is null)) {
            req.parsedPath.delete(0);
            return handler(req);
        } else {
            return false;
        }
    }
}

## Collects the contents in a post/put request prior to calling the user's 
## handler and storing all of the data in the "contents" attribute.
class PostDataCollector : 
    Object 
    @implements Functor1[bool, HTTPRequest] {

    Functor1[bool, HTTPRequest] handler;

    oper init(Functor1[bool, HTTPRequest] handler) : handler = handler {}
    
    oper init(function[bool, HTTPRequest] handler) : 
        handler = Function1[bool, HTTPRequest](handler) {
    }
    
    class __Collector : Object @implements Functor2[void, HTTPRequest, Buffer] {
        StringWriter __writer = {};
        Functor1[bool, HTTPRequest] __handler;
        
        oper init(Functor1[bool, HTTPRequest] handler) :
            __handler = handler {
        }
        
        void oper call(HTTPRequest req, Buffer buf) {
            __writer.write(buf);
            if (buf.size == req.remainingPostData) {
                req.contents = __writer.string();
                debug `calling handler with contents: $(req.contents)\n`;
                __handler(req);
            } else if (buf.size > req.remainingPostData) {
                error I`PostDataCollector received buffer [$buf] which is \
                        greater than remaining post data \
                        ($(req.remainingPostData))`;
            }
        }
    }
    
    bool oper call(HTTPRequest req) {
        if (req.method == 'POST' || req.method == 'PUT') {
            debug `starting collector`;
            req.postDataHandler = __Collector(handler);
            return true;
        } else {
            req.sendReply(500, 'text/plain',
                          FStr() I`Invalid method type $(req.method) for \
                                   handler requiring POST or PUT.`
                          );
            return true;
        }
    }
}

bool __notFound(HTTPRequest req) {
    req.sendReply(
        404, 
        'text/html',
        I'<http>
            <head>
              <title>404 - Page not found</title>
            </head>
            <body>
              <h1>404 - Page not found</h1>
            </body>
          </http>'
    );
    return true;             
}

## The NOT_FOUND handler can be appended to the end of a chain (including the 
## HTTPServer itself, of course) to handle all requests that are not resolved 
## by any of the other handlers by returning a 404 response to the client.
const NOT_FOUND := HandlerFunc(__notFound);


@export_symbols HandlerFunc, RequestHandler;
