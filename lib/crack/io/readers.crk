# Classes for reading high-level stuff from Readers.
# Copyright 2010 Google Inc.

import crack.lang AppendBuffer, Buffer, ManagedBuffer, WriteBuffer;
import crack.io cout, FDReader, Reader;

NEWLINE := '\n'[0];

class LineIter;
class LineReader;
LineIter _createLineIter(LineReader reader);

## Allows you to read one line at a time.
class LineReader {
    Reader r;
    ManagedBuffer buffer = {1024};
    uint start, end;
    
    oper init(Reader reader) : r = reader {}
    
    String readLine() {
        while (true) {
            for (uint i = start; i < end; ++i) {
                if (buffer.buffer[i] == NEWLINE) {
                    tmp := Buffer(buffer.buffer + start, i + 1 - start);
                    start = i + 1;
                    return String(tmp);
                }
            }
            
            # we didn't find a newline, grow the buffer and read another block
            
            # compact the buffer
            if (start) {
                buffer.move(0, start, end - start);
                end = end - start;
                start = 0;
            }
            
            # if there's less than 1K of space available, grow the buffer
            if (end + 1024 > buffer.size)
                buffer.grow(buffer.size + 1024);
            
            # read the next block
            amtRead := r.read(WriteBuffer(buffer.buffer + end, 1024));
            end = end + amtRead;
            
            # if we are at the end of the file, either return the remaining 
            # chunk or a null pointer if there is no remaining chunk
            if (!amtRead)
                if (start == end) {
                    return null;
                } else {
                    result := String(Buffer(buffer.buffer + start, end - start));
                    start = end;
                    return result;
                }
        }
        
        // should never get here
        return null;
    }
    
    LineIter iter() { return _createLineIter(this); }
}

class LineIter {
    LineReader reader;
    String line;
    
    oper init(LineReader reader) : reader = reader, line = reader.readLine() {}
    bool isTrue() { return !(line is null); }
    String elem() { return line; }
    void next() { line = reader.readLine(); }
}

LineIter _createLineIter(LineReader reader) {
    return LineIter(reader);
}

## A FullReader is a Reader that supports line reads, and complete reads of 
## its underlying reader.
class FullReader : LineReader, Reader {
    
    oper init(Reader reader) : LineReader(reader) {}

    ## Read the entire remaining contents of the underlying reader and return 
    ## it as a string.
    String readAll() {
        # start with the current contents of the buffer
        AppendBuffer result = {end - start};
        result.extend(buffer.buffer + start, end - start);
        
        # reset the buffer
        start = 0;
        end = 0;
        
        # use a temporary write buffer so we read in chunks
        buf := WriteBuffer(buffer.buffer, 1024);
        
        # read in the remainder of the file
        amtRead := r.read(buf);
        while (amtRead) {
            result.extend(buf.buffer, amtRead);
            amtRead = r.read(buf);
        }
        
        result.size = result.pos;
        return String(result, true);
    }
}
