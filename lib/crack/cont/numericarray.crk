import crack.lang cmp, free, IndexError, InvalidArgumentError, AssertionError;
import crack.math abs, log2;
import crack.io cout, Writer, FStr;
import crack.algorithm QuickSort;
import crack.cont.array Array, ArrayData;
@import crack.exp.ann define;

void _bind(Object obj) { obj.oper bind(); }
void _release(Object obj) { obj.oper release(); }
bool _isObject(Object obj) { return true; }
bool _isNull(Object obj) {
    return (obj is null);
}

@define _nobind(type) { 
    void _bind(type i) { }
    void _release(type i) { }
    bool _isObject(type i) { return false; }
    bool _isNull(type i) { return false; } 
}

@_nobind(byte)
@_nobind(int)
@_nobind(intz)
@_nobind(int32)
@_nobind(uint)
@_nobind(uintz)
@_nobind(uint32)
@_nobind(int64)
@_nobind(uint64)
@_nobind(float)
@_nobind(float32)
@_nobind(float64)
@_nobind(byteptr)

/// An 1D array that can be assumed to contain elements that have numeric
/// properties
class NumericVector[Elem] : Array[Elem] {

    oper init (uint initCapacity): Array[Elem](initCapacity){
    }

    // This should go away when we have virtual inheritance
    oper init (ArrayData[Elem] data, array[Elem] rep, uint cap, uint size):
        Array[Elem](data, rep, cap, size) {
    }

    @static
    NumericVector copy(NumericVector NV){
        V := NV.copy();
        return NumericVector(V.data, V.data.rep, V.data.cap, V.count());
    }
    
    @static
    NumericVector range(Elem startVal, Elem endVal, Elem delta){
        if (delta <= 0)
            throw InvalidArgumentError('Array.range step size cannot be <= 0');

        int realSize = (endVal - startVal);

        V := NumericVector(realSize);

        if (realSize < 0){
            val := endVal;
            for (uint i = 0; val > startVal; i++){
                V.append(val);
                val -= delta;
            }
        }
        else {
            val := startVal;
            for (uint i = 0; val < endVal; i++){
                V.append(val);
                val += delta;
            }
        }
        return V;
    }
    
    @static
    NumericVector range(uint endVal){
        return range(0, Elem(endVal), 1);
    }

    @static
    NumericVector linspace(Elem startVal, Elem endVal, uint num, bool includeLast){
        Elem delta = 1;
        if (endVal > startVal){
            delta = endVal - startVal;
            delta /= Elem(num);
            if (includeLast)
                endVal += delta;
        }
        else {
            delta = startVal - endVal;
            delta /= Elem(num);
            if (includeLast)
                endVal -= delta;
        }
        return range(startVal, endVal, delta);
    }

    bool _isConformant(NumericVector vector){
        return count() == vector.count();
    }

     void _assertConformant(NumericVector vector){
        if (!_isConformant(vector))
            throw AssertionError('Arrays are not conformant');
    }


    /// Set all elements of the vector to value
    void set(Elem value){
        size := count();
        rep := data.rep;

        for (uint i = 0; i < size; i++)
           rep[i] = value;
    }

    /// Convenience constructor to create a vector filled with the given
    /// value
    @static
    NumericVector empty(uint maxInd){
        V := NumericVector(maxInd);
        V._setSize(maxInd);
        return V;
    }

    @static
    NumericVector empty(NumericVector vector){
        V := NumericVector(vector.count());
        V._setSize(vector.count());
        return V;
    }


    /// Convenience constructor to create a vector filled with the given
    /// value
    @static
    NumericVector fill(uint maxInd, Elem value){
        V := empty(maxInd);
        V.set(value);
        return V;
    }

    @static
    NumericVector fill(NumericVector vector, Elem value){
        V := empty(vector.count());
        V.set(value);
        return V;
    }

    /// Convenience constructor to create a zeroed out vector
    @static
    NumericVector zeros(uint maxInd){
        return fill(maxInd, 0);
    }

    @static
    NumericVector zeros(NumericVector vector){
        return fill(vector, 0);
    }

    /// Convenience constructor to create a vector of ones
    @static
    NumericVector ones(uint maxInd){
        return fill(maxInd, 1);
    }

    @static
    NumericVector ones(NumericVector vector, uint maxInd){
        return fill(vector, 1);
    }

    /// Append all elements in "other" onto the vector.
    void append(NumericVector other) {
        ts := this.count();
        os := other.count();
        if (data.cap - ts < os) {
            newCap := data.cap + os;
            realloc(newCap);
        }
        
        for (intz i = 0; i < os; ++i) {
            elem := other[i];
            data.rep[ts + i] = elem;
            _bind(elem);
        }
        _setSize(ts + os);
    }

    /// Apply scalar operator to all elements
    @define ScalarOperEqual(op){
        void oper op$$=(Elem value){
            size := count();
            rep := data.rep;

            for (uint i = 0; i < size; i++)
               rep[i] = rep[i] op value;
        }
    }

    @ScalarOperEqual(+)
    @ScalarOperEqual(-)
    @ScalarOperEqual(*)
    @ScalarOperEqual(/)

    /// Vector operators
    @define VectorOperEqual(op){
        void oper op$$=(NumericVector vector){
            size := count();
            rep := data.rep;
            rep2 := vector.data.rep;
            _assertConformant(vector);

            for (uint i = 0; i < size; i++)
               rep[i] = rep[i] op rep2[i];
        }
    }

    @VectorOperEqual(+)
    @VectorOperEqual(-)
    @VectorOperEqual(*)
    @VectorOperEqual(/)

}


/// A multidimensional array similar to numpy's ndarray
/// For 1D arrays it is faster to use the above NumericVector class
class NumericArray[Elem] {
    // underlying data
    ArrayData[Elem] data;
    array[Elem] __rep = null; // equal to data.rep
    uint __cap,               // equal to data.cap
         __size;              // size of array in elements
    
    // description of our view of the data
    Array[uint] shape;
    uint dims;
    Array[uintz] strides;
    Array[uintz] offsets;
    
    NumericArray T; // Transpose
    
    @define initFromArray(ArrayType){
        oper init(ArrayType[Elem] A): dims=1 
        {
            cols := A.count();
            data=ArrayData[Elem](cols);
            __rep = data.rep;
            __cap = data.cap; 
            __size = cols;

            // Copy over data
            Arep := A.data.rep;
            for (uint i = 0; i < cols; i++)
                __rep[i] = Arep[i];

            shape = Array[uint]![cols];
            strides = Array[uintz]![1];
            offsets = Array[uintz]![0];
            dims = 1;
            T = this;
        }
    }


    @initFromArray(Array)
    @initFromArray(NumericVector)
    
    void _oneDinitWorker(uint cols){
        data=ArrayData[Elem](cols);
        __rep = data.rep;
        __cap = data.cap; 
        __size = 0;

        shape = Array[uint]![0];
        strides = Array[uintz]![1];
        offsets = Array[uintz]![0];
        dims = 1;
        T = this;

    }

    oper init(){
        _oneDinitWorker(16);
    }
    
    oper init(uint cols){
        _oneDinitWorker(cols);
    }

    void _createTranspose();

    @define initFromArrayArray(ArrayType){
        oper init(ArrayType[ArrayType[Elem]] AA): dims = 2
        {
            if (AA.count() == 0 || AA[0].count() == 0)
                throw AssertionError("Number of rows must be greater than zero");

            cols := AA[0].count();
            uint rows;
            for (rows = 1; rows < AA.count(); rows++){
                if (AA[rows].count() != cols)
                    throw AssertionError("All rows must have the same length");
            }

            
            data=ArrayData[Elem](cols * rows);
            __rep = data.rep;
            __cap = data.cap; 
            __size = cols * rows;

            // Copy over data
            uint k = 0;
            for (uint j = 0; j < rows; j++){
                for (uint i = 0 ; i < cols; i++){
                    __rep[k] = AA[j].data.rep[i];
                    k++;
                }
            }

            shape = Array[uint]![cols, rows];
            strides = Array[uintz]![1, cols];
            offsets = Array[uintz]![0, 0];
            _createTranspose();
            T.T = this;
        }
    }

    @initFromArrayArray(Array)

    // This is a low-level constructor for use when slices, subarrays or transposes
    // are created
    oper init(ArrayData[Elem] data0, Array[uint] shape0,
              Array[uintz] strides0, Array[uintz] offsets0):
              data = data0, shape = shape0, 
              strides = strides0, offsets = offsets0,
              __rep = data0.rep, __cap = data0.cap, dims = shape0.count() {

        dims = shape0.count();
        __size = shape0[0];
        for (uint i = 0; i < shape0.count(); i++)
            __size *= shape0[i];
        T = null;
    }
    
    void _createTranspose() {
        
        if (dims == 1) {
            T = this;
            return;
        }

        if (dims == 2){
            newShape := Array[uint]![shape[1], shape[0]];
            newStrides := Array[uintz]![strides[1], strides[0]];
            newOffsets := Array[uintz]![offsets[1], offsets[1]];
            T = NumericArray(data, newShape, newStrides, newOffsets);
        }
    }
    
    ArrayData[Elem] _copyData(uint newCap){
        newData := ArrayData[Elem](newCap);
        newRep := newData.rep;

        uint i;
        for (uint i = 0; i < __size; i++)
            newRep[i] = __rep[i];

        if (__cap > 0 && _isObject(__rep[0]))
            for (uint i = __size; i < newCap; i++)
                newRep[i] = null;

        return newData;
    }

    @static
    NumericArray I(uint dim){
        newData := ArrayData[Elem](dim*dim);

        uint cnt = 0;
        for (uint j = 0; j < dim; j++)
            for (uint i = 0; i < dim; i++){
                if (i == j) newData.rep[cnt] = 1;
                else newData.rep[cnt] = 0;
                cnt++;
        }
        
        return NumericArray(newData, Array[uint]![dim, dim], 
                                     Array[uintz]![1, dim], 
                                     Array[uintz]![0, 0]);
    }


    @define identityNumbered(dim) {
        @static
        NumericArray I$$dim(){
            return I(dim);
        }
    }


    @identityNumbered(2)
    @identityNumbered(3)
    @identityNumbered(4)

    void set(Elem value){
        uint i;

        for (i = 0; i < __cap; i++)
            __rep[i] = value;
    }
    
    @static
    NumericArray empty(Array[uint] d){
        if (d.count() == 0)
            throw AssertionError("Attempt to create a NumericArray with 0 dimensions");

        uint cap = 1;

        newShape := d;
        newStrides := Array[uintz](d.count());
        newOffsets := Array[uintz](d.count());

        for (uint i=0; i< d.count(); i++){
            newStrides.append(cap);
            newOffsets.append(0);
            cap*=d[i];
        }
        
        newData := ArrayData[Elem](cap);
        return NumericArray(newData, newShape, newStrides, newOffsets);
    }

    @static
    NumericArray fill(Array[uint] d, Elem value){
        A := empty(d);
        A.set(value);
        return A;
    }

    /// Convenience constructor to create a zeroed out 1D array
    @static
    NumericArray zeros(uint maxInd){
        return fill(Array[uint]![maxInd], 0);
    }

    /// Convenience constructor to create a 1D array of ones
    @static
    NumericArray ones(uint maxInd){
        return fill(Array[uint]![maxInd], 1);
    }


    Elem oper [](uint i){
        if (dims!=1) 
            throw InvalidArgumentError(FStr() 
                `Attempting to access element from $(dims)D array using only one index`);
        if (i >= __size)
            throw IndexError(FStr() 
            `Index $i out of bounds for size $__size array`);
        return (__rep + offsets[0] + strides[0] * i)[0];
        
    }

    void oper []=(uint i, Elem e){
        if (dims!=1) 
            throw InvalidArgumentError(FStr() 
                `Attempting to assign element from $(dims)D array using only one index`);
        if (i >= __size)
            throw IndexError(FStr() 
            `Index $i out of bounds for size $__size array`);
        (__rep + offsets[0] + strides[0] * i)[0] = e;
    }


    Elem oper [](uint i, uint j){
        if (dims!=2) 
            throw InvalidArgumentError(FStr() 
            `Attempting to access element from $(dims)D array using 2 indices`);
        if (i * j >= __size || i > shape[0] || j > shape[1])
            throw IndexError(FStr() 
            `Index [$i,$j] out of bounds for array with shape $shape`);
        return (__rep + strides[1] * (j + offsets[0]) + offsets[0] + strides[0] * i)[0];
    }

    Elem oper [](uint i, uint j, uint k){
        if (dims!=3) 
            throw InvalidArgumentError(FStr() 
            `Attempting to access element from $(dims)D array using 3 indices`);
        throw AssertionError("3D array access is not implemented yet");
    }

    int cmp(Object other) {
        if (this is other)
            return 0;
        else if (!this.isa(NumericArray))
            return Object.cmp(other);

        peer := NumericArray.cast(other);

        if (dims != peer.dims)
            throw AssertionError(FStr() `Comparing $(dims)D array with $(peer.dims) array\n`);

        if (dims == 1){
            oc := peer.shape[0];
            c := shape[0];
            for (uint i = 0; i < c; ++i) {
                
                # if I'm longer than other, and we share a common prefix, I'm 
                # greater.
                if (i > oc)
                    return 1;

                # compare the elements
                elemDiff := cmp(this[i], peer[i]);
                if (elemDiff)
                    return elemDiff;
            }
            
            # if other is longer than me, and we share a common prefix, I'm lesser.
            if (oc > c)
                return -1;
            else
                # we're the same length and all elements are equal
                return 0;
        }
        else if (dims == 2){
            peerSize := 0;
            oc := peer.shape[1];
            c := shape[1];
            for (uint j = 0; j < c; j++){
                if (j > oc)
                    return 1;
                od := peer.shape[0];
                d := shape[0];
                for (uint i = 0; i < d; ++i) {
                    
                    # if I'm longer than other, and we share a common prefix, I'm 
                    # greater.
                    if (i > od)
                        return 1;

                    # compare the elements
                    elemDiff := cmp(this[i,j], peer[i,j]); // TODO use faster iteration
                    if (elemDiff)
                        return elemDiff;
                }
                
                # if other is longer than me, and we share a common prefix, I'm lesser.
                if (od > d)
                    return - 1;
            }
            if (oc > c)
                return -1;
            else
                # we're the same length and all elements are equal
                return 0;
        }
        else if (dims ==0 || dims > 2)
            throw AssertionError(FStr() `Comparing $(dims)D arrays is not implemented\n`);
        return 0;
    }

    ArrayData[Elem] _appendWorker(ArrayData[Elem] tmpData, Elem e){
        uint index = offsets[0] + shape[0]*strides[0];
        if (__cap > index){
            tmpData.rep[index] = e;
            return tmpData;
        }
        else {
            uint newCap = __cap > 1 ? __cap + __cap/uint(abs(log2(float(__cap)))) : 10;
            newData := _copyData(newCap);
            newData.rep[index] = e;
            return newData
        }
    }

    /// Append an element to an array, expanding it if needed
    void append(Elem e){
        if (dims > 1)
            throw InvalidArgumentError(FStr() `Cannot append an element to a ($dims)D array`);

        ArrayData[Elem] newData  = _appendWorker(data, e);

        if (newData != data) {
            __cap = newData.cap;
            __rep = newData.rep;
            data = newData;
        }

        __size++;
        shape[0] = __size;
    }

    /// Return a new array with an element appended
    NumericArray appended(Elem e){
        if (dims > 1)
            throw InvalidArgumentError(FStr() `Cannot append an element to a ($dims)D array`);

        uint index = offsets[0] + shape[0]*strides[0];
        ArrayData[Elem] newData;
        if (__cap > index) {
            newData = _copyData(__cap);
            tmpData := newData;
            newData = _appendWorker(tmpData, e);
            if (!(newData is tmpData))
                throw AssertionError("Array data changed during element append without expansion");
        }
        else
            newData = _appendWorker(data, e);
        if (newData is data)
            throw AssertionError("Array data not copied during element append with expansion");

        return NumericArray(newData, Array[uint]![shape[0] + 1], strides.copy(), offsets.copy());
    }

    NumericArray promoted(){
        if (dims == 1) {
            newShape := Array[uint]![shape[0], 1];
            newStrides := Array[uintz]![strides[0], shape[0]];
            newOffsets := Array[uintz]![offsets[0], 0];

            return NumericArray(data, newShape, newStrides, newOffsets);
        }
        else
            throw AssertionError("promote only implemented for 1D arrays");
    }

    NumericArray flattened(){
        if (dims == 1) return this;
        
        if (dims == 2){
            if (shape[1] > 1 && (offsets[0] > 0 || offsets[1] > 0 || strides[1] != shape[0]))
                throw AssertionError("2D subarray with this shape and offsets cannot be flattened, create a copy first");
            newShape := Array[uint]![shape[0] * shape[1]];
            newStrides := Array[uintz]![strides[0]];
            newOffsets := Array[uintz]![0];

            return NumericArray(data, newShape, newStrides, newOffsets);
        }
        else
            throw AssertionError("flatten only implemented for 1D and 2D arrays");
    }

    void _writeLevel(Writer out, Array[uintz] wstrides, Array[uintz] woffsets, 
                Array[uint] wshape, uint dim);

    void _writeLevel(Writer out, Array[uintz] wstrides, Array[uintz] woffsets, 
                Array[uint] wshape, uint dim){
        out.write('[');
        if (dim == 1){
            bool first = true;
            uintz offset = woffsets[dim - 1];
            uintz stride = wstrides[dim - 1];
            for (uint i = 0; i < wshape[0]; i++){
                if (!first)
                    out.write(', ');
                else
                    first = false;
                Elem elem = (__rep + offset + stride * i)[0];
                if (_isNull(elem)) out.write('null');
                else {
                    out.write(FStr() `$elem`);
                }
            }
        } 
        else {
            bool first = true;

            Array[uintz] noffsets = woffsets.subarray(0, dim - 1).copy();
            nstrides := wstrides.subarray(0, dim - 1);
            nshape := shape.subarray(0, dim - 1);
            stride := wstrides[dim - 1];

            for (uint i = 0; i < wshape[dim - 1]; i++){
                if (!first)
                    out.write(', ');
                else
                    first = false;
                _writeLevel(out, nstrides, noffsets, nshape, dim - 1);
                uintz noffset = noffsets[dim - 2] + stride;
                noffsets[dim - 2] = noffset;
            }
            
        }
        
        out.write(']');
    }

    void writeTo(Writer out) {
        _writeLevel(out, strides, offsets, shape, dims);
    }
    
    oper del(){
        T = null;
    }
}
