# Copyright 2010 Google Inc.
# Implements some basic annotations that no one should be without.  Included
# are:
# @define name(arg, ...) { contents }
#   defines a macro with the specified arguments (you can then use the macro 
#   as @name(args...))
# I guess there's currently only one :-)

import crack.lang die, CString;
import crack.io cout, StringFormatter, Formatter, Writer;
import crack.compiler CrackContext, Token, Location, TOK_STRING;
import crack.container Array, TreeMap;

class UInt {
    uint val;
    
    oper init(uint val0) : val = val0 {}
}

class Node {
    Node next;
    
    void expand(CrackContext ctx, Array args) {}
    String toString(Array args) {return null;}
    Location getLocation() {
        die('Node.getLocation() undefined');
        return null;
    }
    
    void injectRepr(CrackContext ctx, int lvl) {
        die('Node.writeRepr() undefined');
    }
}

class NodeIter {
    Node node;
    oper init(Node node0) : node = node0 {}
    void next() { node = node.next; }
    bool isTrue() { return node; }
    Node elem() { return node; }
}

## A list of tokens.  These are typically stored in reverse order of the 
## actual order of the tokens so that they can be easily "put back" into the 
## token stream (after putBack() they will be in the correct order once again)
class NodeList {
    Node first, last;
    
    void pushHead(Node node) {
        if (first) {
            node.next = first;
            first = node;
        } else {
            first = last = node;
        }
    }    
    
    void append(Node node) {
        if (last) {
            last.next = node;
            last = node;
        } else {
            first = last = node;
        }
    }
    
    Node popHead() {
        if (first) {
            if (last is first)
                last = null;
            result := first;
            first = first.next;
            return result;
        } else {
            return null;
        }
    }

    NodeIter iter() { return NodeIter(first); }

    ## Node lists are true if they have any contents.
    bool isTrue() { return first; }
    
    void injectRepr(CrackContext ctx, int lvl) {
        
        # we should never end up here recursively because there cannot be 
        # nested concats.  If this situation changes, we need to do something 
        # more complicated here to allow nested list construction.
        if (lvl) die('NodeList.injectRepr() called recursively');

        StringFormatter out;

        # inject the code to create the elements of the list.
        for (node :in this) {
            out = StringFormatter();
            f := @FILE; l := @LINE; out `list.pushHead(node);`;
            ctx.inject(f.buffer, l, out.createCString().buffer);
            node.injectRepr(ctx, ++lvl);
        }
        
        # inject the code to create the list.
        out = StringFormatter();
        f := @FILE; l := @LINE; out `list = NodeList();`;
        ctx.inject(f.buffer, l, out.createCString().buffer);
    }
            
}

class Tok : Node {
    Token tok;

    oper init(Token tok0) : tok = tok0 {}

    void expand(CrackContext ctx, Array args) {
        ctx.putBack(tok);
    }
    
    String toString(Array args) {
        if (tok.isString())
            return String(tok.getText()).getRepr();
        else
            return String(tok.getText());
    }
    
    Location getLocation() {
        return tok.getLocation();
    }
    
    void writeTo(Writer out) {
        out.write('token: ' + String(tok.getText()));
    }

    void injectRepr(CrackContext ctx, int lvl) {
        StringFormatter out = {};
        loc := tok.getLocation();
        f := @FILE; l := @LINE; out `
            node = Tok(Token($(tok.getType()), 
                       $(String(tok.getText()).getRepr()).buffer,
                       ctx.getLocation(
                            $(String(loc.getName()).getRepr()).buffer, 
                            $(loc.getLineNumber())
                            )
                       )
                 );`;
        ctx.inject(f.buffer, l, out.createCString().buffer);
    }
}

class Arg : Node {
    uint argIndex;
    Location loc;
    
    oper init(Location loc0, uint argIndex0) : 
        loc = loc0, 
        argIndex = argIndex0 {
    }

    void expand(CrackContext ctx, Array args) {
        for (node :in NodeList.cast(args[argIndex]))
            node.expand(ctx, args);
    }
    
    String toString(Array args) {
        StringFormatter sf = {};
        argVal := NodeList.cast(args[argIndex]);
        
        # do the last node so we can add a whitespace separator for every 
        # subsequent node.
        if (argVal.last)
            sf.format(argVal.last.toString(args));
        
        # do the rest of the nodes
        void reverseNodeList(Array args1, StringFormatter out, Node node) {
            
            # stop recursing before the last node
            if (node.next) {
                reverseNodeList(args1, out, node.next);
                out ` $(node.toString(args1))`;
            }
        }

        reverseNodeList(args, sf, argVal.first);
        return sf.createCString();
    }
    
    void writeTo(Writer out) { Formatter(out) `arg: $argIndex\n`; }
    Location getLocation() { return loc; }
    
    void injectRepr(CrackContext ctx, int lvl) {
        StringFormatter out = {};
        f := @FILE; l := @LINE; out `
            node = Arg(ctx.getLocation($(String(loc.getName()).getRepr()).buffer,
                                       $(loc.getLineNumber())
                                       ),
                       $argIndex
                       );`;
        ctx.inject(f.buffer, l, out.createCString().buffer);
    }
}

## Concat nodes concatenate a set of tokens into a single token.  The type and 
## location come from the first non-argument token.
class Concat : Node {
    NodeList list;
    int type;
    Location loc;

    oper init(NodeList list0, Location loc0) : 
        loc = loc0,
        list = list0 {
    }

    void expand(CrackContext ctx, Array args) {
        StringFormatter concat = {};
        for (item :in list)
            concat.format(item.toString(args));
        concat.format(' \0');
        
        s := concat.createString();
        ctx.inject(@FILE.buffer, @LINE, s.buffer);
    }
    
    String toString(Array args) {
        die('Concat.toString() called.');
        return null;
    }
    
    Location getLocation() {
        return loc;
    }

    void injectRepr(CrackContext ctx, int lvl) {
        StringFormatter out = {};

        f := @FILE; l := @LINE; out `
            node = Concat(list,
                          ctx.getLocation(
                            $(String(loc.getName()).getRepr()).buffer,
                            $(loc.getLineNumber())
                            )
                          );`;
        ctx.inject(f.buffer, l, out.createCString().buffer);
        list.injectRepr(ctx, lvl);
    }
}

class Stringify : Node {
    Node node;
    
    oper init(Node node0) : node = node0 {}
    
    void expand(CrackContext ctx, Array args) {
        ctx.putBack(Token(TOK_STRING, node.toString(args).buffer, 
                          node.getLocation()
                          )
                    );
    }
    
    String toString(Array args) {
        die('Stringify.toString called.');
        return null;
    }
    
    Location getLocation() {
        return node.getLocation();
    }
    
    void injectRepr(CrackContext ctx, int lvl) {
        StringFormatter out = {};
        f := @FILE; l := @LINE; out `node = Stringify(node);`;
        ctx.inject(f.buffer, l, out.createCString().buffer);
        node.injectRepr(ctx, lvl);
    }
}

## Parse macro arguments, returns them as an array of NodeList objects.
Array parseArgs(CrackContext ctx, uint argCount) {
    # extract the arguments from the following tokens
    tok := ctx.getToken();
    if (!tok.isLParen())
        ctx.error(tok, 'left paren expected in macro expansion'.buffer);
    
    uint nesting = 1;
    argTokList := NodeList();
    Array args = {argCount};
    while (true) {
        tok = ctx.getToken();
        if (nesting == 1 && tok.isComma()) {
            args.append(argTokList);
            argTokList = NodeList();
        } else {
            if (tok.isLParen()) {
                ++nesting;
            } else if (tok.isRParen()) {
                if (!--nesting)
                    break;
            }

            argTokList.pushHead(Tok(tok));
        }
    }
    
    # if we got a non-empty element left over, add it to the list.
    if (argTokList.first)
        args.append(argTokList);
    
    # verify that the number of arguments provided was correct
    if (args.count() != argCount) {
        fmt := StringFormatter();
        fmt `Incorrect number of arguments for macro: expected \
$(argCount), got $(args.count())\0`;
        ctx.error(tok, fmt.createString().buffer);
    }

    return args;
}

class Macro : NodeList {
    String name;
    uint argCount;
    
    void expand(CrackContext ctx) {
        # parse the args.
        args := parseArgs(ctx, argCount);

        # expand the macro
        Node node = first;
        while (node) {
            node.expand(ctx, args);
            node = node.next;
        }
    }    
}

## Annotation to expand a macro.  This expects a Macro instance as user data.
void expand(CrackContext ctx) {
    mac := Macro.unsafeCast(ctx.getUserData());
    mac.expand(ctx);
}

# macro parser states
ST_NONE := 0;
ST_ESC := 1;   # got a dollar-sign escape
ST_CONCAT := 2;  # got a double-dollar-sign concatenation operator

void define(CrackContext ctx) {
    Macro mac = {};
    TreeMap args = {};
    uint index;
    tok := ctx.getToken();
    
    if (!tok.isIdent())
        ctx.error(tok, 'Macro name expected after define.'.buffer);
    mac.name = String(tok.getText());
    
    # parse the '(' that begins the argument list
    tok = ctx.getToken();
    if (!tok.isLParen())
        ctx.error(tok, 'Left paren expected after macro definition'.buffer);
    
    # read all of the arguments
    while (true) {
        tok = ctx.getToken();
        if (tok.isRParen())
            break;
        
        if (!tok.isIdent())
            ctx.error(tok, 'identifier expected'.buffer);

        args[String(tok.getText())] = UInt(index++);
        
        tok = ctx.getToken();
        if (tok.isRParen())
            break;
        else if (!tok.isComma())
            ctx.error('comma or end paren expected in macro def'.buffer);
    }
    mac.argCount = index;
    
    # read the body of the macro
    tok = ctx.getToken();
    if (!tok.isLCurly())
        ctx.error('Expected left bracket'.buffer);

    # read everything until the end bracket
    uint bracketCount = 1;
    int state = ST_NONE;
    NodeList lastConcat = {};  # last concatenation sequence.
    Node node;  # the last node
    while (true) {
        tok = ctx.getToken();
        Object argIndex;
        
        if (tok.isEnd()) {
            ctx.error(
                'End of file before the end of a macro definition'.buffer
            );
        # check for brackets, increment or decrement the bracket count.
        } else if (tok.isRCurly()) {
            if (!--bracketCount)
                break;
        } else if (tok.isLCurly()) {
            ++bracketCount;
        } else if (tok.isDollar()) {
            if (state == ST_NONE) {
                state = ST_ESC;
            } else if (state == ST_ESC) {
                if (!node)
                    ctx.error('The macro may not begin with a concatenation '
                               'operator'.buffer
                              );
                
                # if we're starting off a new concatenation sequence, add the 
                # last node and store the sequence as a new node in the macro.
                if (!lastConcat) {
                    lastConcat.append(node);
                    mac.popHead();
                    mac.pushHead(Concat(lastConcat, tok.getLocation()));
                }

                state = ST_CONCAT;
            } else {
                ctx.error('One two many dollar signs\n'.buffer);
            }
            continue;
        }

        # replace arguments with indices
        if (tok.isIdent() && (argIndex = args.get(String(tok.getText()))))
            node = Arg(tok.getLocation(), UInt.cast(argIndex).val);
        else
            node = Tok(tok);

        # handle special states
        if (state == ST_CONCAT) {
            
            # concatenation state - add the new node to the concatenation 
            # sequence.
            lastConcat.append(node);
            state = ST_NONE;
        } else if (state == ST_ESC) {
            
            # stringify state - add a stringify node.
            mac.pushHead(Stringify(node));
            state = ST_NONE;
        } else {
            mac.pushHead(node);
            
            # reset any last concatenation sequence that we've accumulated
            if (lastConcat)
                lastConcat = NodeList();
        }
    }
    
    # store a new annotation for expanding the macro
    ctx.storeAnnotation(mac.name.buffer, expand, mac);
    mac.oper bind();
}

## Export a macro, making it visible from this module.
void export(CrackContext ctx) {
    
    # get the next token, make sure it's an annotation.
    tok := ctx.getToken();
    if (!tok.isIdent())
        ctx.error(tok, 'Identifier expected'.buffer);
    name := CString(tok.getText(), false);
    ann := ctx.getAnnotation(name.buffer);
    Macro mac;
    if (ann is null) {
        StringFormatter fmt = {};
        fmt `$name is not an annotation.\0`;
        ctx.error(tok, fmt.createString().buffer);
    } else {
        mac = Macro.unsafeCast(ann.getUserData());
    }
    
    StringFormatter code = {};
    # inject the bottom code first (the token stream is LIFO)

    f := @FILE; l := @LINE; code `
                ctx.storeAnnotation($(name.getRepr()).buffer, $name,
                                    mac
                                    );
            } else {
                mac = Macro.unsafeCast(userData);
            }
            
            mac.expand(ctx);
        }
    \n\0`;
    ctx.inject(f.buffer, l, code.createString().buffer);

    # generate code for the tokens
    for (node :in mac) {
        
        # inject the code to insert the node
        code = StringFormatter();
        f = @FILE; l = @LINE; code `mac.pushHead(node);\n`;
        ctx.inject(f.buffer, l, code.createCString().buffer);
        
        # inject the code to generate the node.
        node.injectRepr(ctx, 0);
    }
    
    code = StringFormatter();
    f = @FILE; l = @LINE; code `
        void $name(CrackContext ctx) {
            userData := 
                ctx.getUserData();
            Macro mac;
            if (userData is null) {
                mac = Macro();
                mac.argCount = $(mac.argCount);
                Node node;
                NodeList list;
    \n\0`;
    
    t := code.createString();
    ctx.inject(f.buffer, l, t.buffer);
}

## Call this annotation at the top of your file if you want to export macros.  
## It will import everything you need.
void exporter(CrackContext ctx) {
    StringFormatter f = {};
    f `import crack.exp.ann Macro, Node, NodeList, Tok, Arg, Stringify, Concat;
       import crack.compiler CrackContext, Token, TOK_STRING`;
    ctx.inject(@FILE.buffer, @LINE, 
               f.createCString().buffer);
}
