// A test suite for crack
// Copyright 2010-2011 Shannon Weyrick <weyrick@mozek.us>

import crack.io cout, cerr, Formatter, Writer, StringWriter, StringFormatter;
import crack.sys argv, exit;
import crack.container List, Iterator, TreeMap, KeyVal;
import crack.exp.dir Directory;
import crack.exp.file File, FileInfo;
import crack.exp.regex Regex;
import crack.exp.process Process;

SCREEN_VERSION := ".4";

class Config {
    String crackBin, rootDir, outDir;  
    bool verbose;
}
Config config = {};

class Test {

    FileInfo _fInfo;
    String _desc;
    TreeMap _testData = {};
    
    String _isSkipped;

    oper init(): _desc("Unknown") { }

    void loadTemplate() {
        
        f := File(_fInfo.name, "r");
        if (!f.isValid()) {
            cout `file: $(_fInfo.name) could not be opened!\n`;
            _isSkipped = "unable to open";
            return;
        }
        
        // parse out sections
        String lineBuf, curSection;
        StringWriter dataBuf = {};
        headerPat := Regex('^--(\\w+)--$');
        while (lineBuf = f.nextLine()) {
            // look for section change
            m := headerPat.search(lineBuf);
            if (m) {
                if (curSection)
                    _testData[curSection] = dataBuf.createString();
                curSection = m.group(1);
                //cerr `debug: switching to group $curSection\n`;
                dataBuf = StringWriter();
            }
            else {
                // add line to current section
                dataBuf.write(lineBuf);
                if (curSection == 'TEST')
                    _desc = String(lineBuf, 0, lineBuf.size-1);
            }
        }
        if (curSection)
            _testData[curSection] = dataBuf.createString();

    }

    /// ensure test has required sections and that they contain valid data
    /// also check to see if we're skipping this test for some reason
    void checkTemplate() {        
        if (!_testData["FILE"]) {
            _isSkipped = "the template was missing a FILE section";
        }
    }

    String writeScript(Directory outDir) {
        
        outName := outDir.nameWithTrailing()+_fInfo.basename(true) + ".crk";
        
        //cout `writing: $outName\n`;
        
        out := File(outName,"w");        
        out.write(String.cast(_testData["FILE"]));

        return outName;
        
    }

    String _colorMsg(String c, String msg) {
        sw := StringFormatter(128);        
        sw `\033[$(c);1m$(msg)\033[0m`;
        return sw.createString();
    }
    String _pass() {
        return _colorMsg("32", "PASS");
    }
    String _fail() {
        return _colorMsg("31", "FAIL");
    }

    void compareOutput(String expected, String actual) {
        
        if (expected == actual) {
            cout `$(_pass())`;
        }
        else {
            cout `$(_fail())`;
            if (config.verbose) {
                cout `Expected:\n[$expected]\nActual:\n[$actual]\n`;
            }
            // XXX check config object
            // XXX optionally show diff, or fail after failed test
        }
        
    }

    void execCrackWithBuilder(String builder, String nick, 
                              Directory outDir, bool nativeRun) {
        
        testScript := writeScript(outDir);
        String testBinary;
        
        String args = {};
        if (_testData["ARGS"]) {
            args = String.cast(_testData["ARGS"]).rtrim();            
        }

        if (nativeRun) {
            // we need to output a binary, then run it after for the final results
            testBinary = outDir.nameWithTrailing()+_fInfo.basename(true);
            args = args+" -b out="+testBinary;
        }

        // XXX script argv
        cmd := config.crackBin+" -B "+builder+" "+args+" "+testScript;
        if (config.verbose)
            cout `(running: $cmd])\n`;

        cout `$nick: `;
        if (nativeRun)
            cout `(Compile: `;
        
        // XXX stdin
        p := Process(cmd);
        p.wait();
        compileOut := p.getStdOut();
        compileErr := p.getStdErr();

        if (p.getReturnCode() != 0) {
            // compile failed
            cout `$(_fail()) `;
            cout `[$(p.getReturnCode())]\n$compileErr`;
            return;
        }
        else if (nativeRun) {
            cout `$(_pass()) `;
        }
        
        if (!_testData["EXPECT"]) {
           cout `no valid EXPECT section\n`;
           return;
        }

        expect := String.cast(_testData["EXPECT"]);
        if (nativeRun) {
            // native: run binary to get final results for comparison
            bfi := FileInfo(testBinary);
            if (!bfi.exists()) {
                cout `$nick: `;
                _fail();
                cout `: binary ($testBinary) wasn't created, check compile output\n`;
                return;
            }
            // XXX binary args (script argv)
            if (config.verbose)
                cout `(running: $testBinary)\n`;
            cout `Run: `;
            b := Process(testBinary);
            b.wait();
            if (b.failed()) {
                cout `$(_fail())`;
                cout `: binary failed to run\n`;
            }
            else {
                nativeOut := b.getStdOut();
                nativeErr := b.getStdErr();
                compareOutput(expect, nativeOut);
                // XXX err output EXPECTERR ?
            }
            cout `) `;
        }
        else {
            // JIT, interpreted, etc
            compareOutput(expect, compileOut);
            cout `  `;
            // XXX err output EXPECTERR ?
        }
        
    }

    void run(Directory outDir) {
        
        if (_isSkipped) {
            cout `[$_fInfo]: $_isSkipped\n`;
            return;
        }
        
        cout `[$_fInfo]: $_desc\n`;
        
        execCrackWithBuilder("llvm-jit", "JIT", outDir, false);
        execCrackWithBuilder("llvm-native", "Native", outDir, true);
        
        cout `\n`;

    }

    oper init(FileInfo info): _fInfo = info,
                              _desc("Unknown") {
        loadTemplate();
        checkTemplate();
    }

    void _dump(Writer out) {
        ti := _testData.iter();
        while (ti.nx()) {
            section := String.cast(KeyVal.cast(ti.elem()).key);
            data := String.cast(KeyVal.cast(ti.elem()).val);
            Formatter(out) `[$section]:\n$data`;
        }
    }

    void writeTo(Writer out) {
        Formatter(out) `[$_fInfo]: $_desc\n`;
        //_dump(out);
    }

}

class TestSuite {

    List _testList = {};
    
    void usage() {
        cout `screen <crack binary> <test dir> <output dir>\n`;
        exit(1);
    }
    
    void scanForTests(Directory dir) {
    
        if (!dir.isValid()) {
            cerr `skipping invalid directory: $dir\n`;
            return;
        }
        
        cout `importing tests from $dir ... `;

        Iterator i;
        cnt := 0;
        
        // gather test files
        FileInfo curFile;
        i = dir.fileInfoIter();
        while (i.nx()) {
            curFile = FileInfo.cast(i.elem());
            if (curFile.matches("*.crkt")) {
                cnt++;
                test := Test(curFile);
                _testList.append(test);
            }
        }

        cout `found $cnt\n`;

        // recurse to directories
        Directory nextDir;
        i = dir.dirIter();
        while (i.nx()) {
            nextDir = Directory.cast(i.elem());
            scanForTests(nextDir);
        }
    }

    void runTests(Directory dir) {

        if (!dir.isValid()) {
            cerr `invalid output directory: $dir\n`;
            return;
        }
        
        cout `\nrunning tests with output in $dir...\n`;

        Test curTest;

        i := _testList.iter();
        while (i.nx()) {
            curTest = Test.cast(i.elem());
            curTest.run(dir);
        }

    }

    oper init() {
        
        if (argv.count() <= 3) {
            usage();            
        }

        cout `screen v$(SCREEN_VERSION)\n`;

        config.crackBin = argv[1];
        config.rootDir = argv[2];
        config.outDir = argv[3];
 
        // XXX
        //config.verbose = 1;
        
        // check binary
        cfi := FileInfo(config.crackBin);
        if (!cfi.exists()) {
            cout `crack binary [$(config.crackBin)] not found\n`;
            exit(1);            
        }
        
        p := Process(config.crackBin+" --version");
        p.wait();
        String out = {};
        if (!p.failed())
            out = p.getStdOut();        
        if (p.failed() || p.getReturnCode() != 0) {
            cout `crack binary [$(config.crackBin)] failed to run, or unexpected result\n`;
            exit(1);
        }
        else if (out.size < 5 || out.slice(0,5) != "crack") {
            cout `crack binary [$(config.crackBin)] invalid version response: $(out.slice(0,5))\n`;
            exit(1);
        }
        else {
            cout `using binary: $out\n`;
        }
        
        scanForTests(Directory(config.rootDir));

        runTests(Directory(config.outDir, false));
        
    }

}

suite := TestSuite();
