%%TEST%%
comm test
%%ARGS%%
%%FILE%%
import crack.eventmgr EventCallbackFunc;
import crack.io cerr, Formatter, FStr, StandardFormatter, Writer;
import crack.lang Buffer;
import crack.net InetAddress, Poller, Socket, SocketApd, AF_INET,
    SOCK_STREAM;
import crack.net.comm Comm, CommHandler, CommListener, Log, SyncHandler,
    Waiter, WaiterFor;
import crack.threads Event;
import crack.time add, Time, TimeDelta;

@import crack.ann assert;

# Change to cerr for verbose logging output.
log := Formatter.unsafeCast(null);
#log := cerr;

import crack.threads Condition, Mutex, MutexLock;

class EchoCommHandler : SyncHandler {
    bool server;
    WaiterFor[String, int] waiter;

    oper init(Socket sock, Object connectionId, Formatter error,
              Formatter info,
              Formatter debug,
              bool server
              ) :
        SyncHandler(sock, connectionId, error, info, debug),
        server = server {
    }

    oper init(Object connectionId, Formatter log) :
        SyncHandler(connectionId, log, log, log) {
    }

    void process() {
        _info `in $(server ? 'server' : 'client') process`;
        if (!inBuf.size)
            return;

        # Quit if we don't have a complete message.
        size := inBuf[0];
        if (inBuf.size < size + 1)
            return;

        if (server) {
            outBuf.extend(Buffer(inBuf.buffer, size + 1));
            inBuf.compact(size + 1);
        } else if (waiter) {
            _info `sending response back to waiter\n`;
            waiter.setResult(String(inBuf.buffer, size + 1, false));
            inBuf.compact(size + 1);
        } else {
            _info `client has no waiter!!!\n`;
        }
    }

    void addWaiter(Waiter waiter) {
        this.waiter = WaiterFor[String, int].cast(waiter);
    }
}

class EchoListener : CommListener {
    oper init(Socket sock, Object connectionId, Formatter error,
              Formatter info,
              Formatter debug
              ) :
        CommListener(sock, connectionId, error, info, debug) {
    }

    oper init(Object connectionId, Formatter log) :
        CommListener(connectionId, log, log, log) {
    }

    CommHandler makeCommHandler(Socket.Accepted accepted) {
        return EchoCommHandler(accepted.sock, FStr() `$_serverId-handler`,
                               _error, _info,
                               _debug,
                               true
                               );
    }
}

poller := Poller();

# Set port number to zero, allow the system to assign it.
const ADDR := InetAddress(127, 0, 0, 1, 0);

server := Socket(AF_INET, SOCK_STREAM, 0);
server.setReuseAddr(true);
server.bind(ADDR);
server.listen(5);
SocketApd(server).getSocketAddress(ADDR);  # Fill in the port number.
poller.add(server, EchoListener(server, 'server', log, log, log));

clientSock := Socket(AF_INET, SOCK_STREAM, 0);
@assert(clientSock.connect(ADDR));
client := EchoCommHandler(clientSock, 'client', log, log, log, false);
poller.add(clientSock, client);

client.outBuf.extend('\x0Bhello world');
int i;
for (i = 0; i < 100; ++i) {
    poller.waitAndProcess(null);
    if (client.inBuf == '\013hello world')
        break;
}

if (i == 100)
    cerr `FAILED round trip communication test\n`;

client.close(poller);
for (i = 0; i < 100; ++i) {
    poller.waitAndProcess(null);
    if (poller.count() == 1)
        break;
}

if (i == 100)
    cerr `FAILED client close\n`;

poller.remove(server);
server.close();
poller = null;

comm := Comm(Log(log, log, log, log, log));
comm.start();
comm.startServer(ADDR, EchoListener('server2', log), true);
proxy := comm.getSyncProxy(ADDR, client = EchoCommHandler('client2', log));
waiter := WaiterFor[String, int](0);
const TEST_MESSAGE := '\x0Ctest message';
proxy.sendSync(TEST_MESSAGE, waiter);
@assert(waiter.getResult() == TEST_MESSAGE);

if (true) {
    # Schedule an event, make sure that we get it within 3/10ths of a second
    # (hopefully this won't be too flakey).
    Event myEventWaiter = {};
    void myEvent() {
        myEventWaiter.signal();
    }
    schedTime := Time.now();
    comm.scheduleEvent(add(schedTime, TimeDelta(0, 100000000)),
                       EventCallbackFunc(myEvent)
                       );
    myEventWaiter.wait();
    endTime := Time.now();
    diff := endTime - schedTime;
    @assert(diff.secs == 0 && diff.nsecs >= 100000000);
    # This check is likely to be flakey.
    @assert(diff.nsecs < 300000000);
}

proxy.close();
comm.stop();

cerr `ok\n`;

%%EXPECT%%
ok
%%STDIN%%
