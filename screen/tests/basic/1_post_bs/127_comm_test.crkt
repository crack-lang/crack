%%TEST%%
comm test
%%ARGS%%
%%FILE%%
import crack.io cerr, Formatter, FStr, StandardFormatter, Writer;
import crack.lang Buffer;
import crack.net InetAddress, Poller, Socket, AF_INET, SOCK_STREAM;
import crack.net.comm Comm, CommHandler, CommListener, Log;

@import crack.ann assert;

# Change to cerr for verbose logging output.
log := Formatter.unsafeCast(null);
#log := cerr;

import crack.threads Condition, Mutex, MutexLock;
class Waiter {
    String __response;
    Mutex __mutex = {};
    Condition __cond;

    oper init() : __cond(__mutex) {}

    void sendResponse(String response) {
        MutexLock lock = {__mutex};
        __response = response;
        __cond.signal();
    }

    String getResponse() {
        MutexLock lock = {__mutex};
        __cond.wait();
        return __response;
    }
}

class EchoCommHandler : CommHandler {
    bool server;
    Waiter waiter;

    oper init(Socket sock, Object connectionId, Formatter error,
              Formatter info,
              Formatter debug,
              bool server
              ) :
        CommHandler(sock, connectionId, error, info, debug),
        server = server {
    }

    oper init(Object connectionId, Formatter log) :
        CommHandler(connectionId, log, log, log) {
    }

    void process() {
        _info `in $(server ? 'server' : 'client') process`;
        if (!inBuf.size)
            return;

        # Quit if we don't have a complete message.
        size := inBuf[0];
        if (inBuf.size < size + 1)
            return;

        if (server) {
            outBuf.extend(Buffer(inBuf.buffer, size + 1));
            inBuf.compact(size + 1);
        } else if (waiter) {
            _info `sending response back to waiter\n`;
            waiter.sendResponse(String(inBuf.buffer, size + 1, false));
            inBuf.compact(size + 1);
        } else {
            _info `client has no waiter!!!\n`;
        }
    }
}

class EchoListener : CommListener {
    oper init(Socket sock, Object connectionId, Formatter error,
              Formatter info,
              Formatter debug
              ) :
        CommListener(sock, connectionId, error, info, debug) {
    }

    oper init(Object connectionId, Formatter log) :
        CommListener(connectionId, log, log, log) {
    }

    CommHandler makeCommHandler(Socket.Accepted accepted) {
        return EchoCommHandler(accepted.sock, FStr() `$_serverId-handler`,
                               _error, _info,
                               _debug,
                               true
                               );
    }
}

poller := Poller();

const ADDR := InetAddress(127, 0, 0, 1, 49123);

server := Socket(AF_INET, SOCK_STREAM, 0);
server.setReuseAddr(true);
server.bind(ADDR);
server.listen(5);
poller.add(server, EchoListener(server, 'server', log, log, log));

clientSock := Socket(AF_INET, SOCK_STREAM, 0);
@assert(clientSock.connect(InetAddress(127, 0, 0, 1, 49123)));
client := EchoCommHandler(clientSock, 'client', log, log, log, false);
poller.add(clientSock, client);

client.outBuf.extend('\x0Bhello world');
int i;
for (i = 0; i < 100; ++i) {
    poller.waitAndProcess(null);
    if (client.inBuf == '\013hello world')
        break;
}

if (i == 100)
    cerr `FAILED round trip communication test\n`;

client.close(poller);
for (i = 0; i < 100; ++i) {
    poller.waitAndProcess(null);
    if (poller.count() == 1)
        break;
}

if (i == 100)
    cerr `FAILED client close\n`;

poller.remove(server);
server.close();
poller = null;

comm := Comm(Log(log, log, log, log, log));
comm.start();
comm.startServer(ADDR, EchoListener('server2', log), true);
proxy := comm.getProxy(ADDR, client = EchoCommHandler('client2', log));
client.waiter = Waiter();
const TEST_MESSAGE := '\x0Ctest message';
proxy.send(TEST_MESSAGE);
@assert(client.waiter.getResponse() == TEST_MESSAGE);

proxy.close();
comm.stop();

cerr `ok\n`;
%%EXPECT%%
ok
%%STDIN%%
