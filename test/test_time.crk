// Test time.h binding in runtime
import crack.runtime InternalDate, ctime, strftime, asctime, epoch;
import crack.exp.cmdline CmdOptions;
import crack.io cout;
import crack.time Date;
import crack.sys argv;

sys_options :=  CmdOptions("--verbose=b/false");
sys_options.parse(argv);

void printDate(InternalDate d){
   cout `date = $(d.tm_mday)/$(d.tm_mon+1)/$(d.tm_year+1900) `
        `$(d.tm_hour):$(d.tm_min):$(d.tm_sec), $(d.tm_wday)/w and $(d.tm_yday)/y, `
        `DST = $(d.tm_isdst)\n`;
}

InternalDate mytime = {};

verbose := sys_options.getBool("v");

mytime.setLocalNow();
if (verbose) printDate(mytime);

mytime.setUTCNow();
if (verbose) printDate(mytime);

secs:=mytime.getSeconds();
if (verbose) cout `secs = $secs\n`;

dateString := String(100,0); // Create a string to store time repr in
asctime(mytime, dateString.buffer);
if (verbose) cout `asctime = $dateString`;

dateString2 := String(100,0);
ctime(secs, dateString2.buffer);
if (verbose) cout `ctime = $dateString2`;

if (dateString!=dateString2)
   cout `Date format mismatch\n`;

mytime.setUTCSeconds(0);
mytime.getSeconds();

len:=strftime(dateString2.buffer, 100, "%A %B %d %Y".buffer, mytime);
if (dateString2.substr(0,len)!='Thursday January 01 1970')
   cout `InternalDate.strftime format mismatch, got $dateString2, len = $len\n`;

// Repeat the above with the wrapped Date class
Date Mytime = {};

Mytime.setLocalNow();
if (verbose) printDate(Mytime);

Mytime.setUTCNow();
if (verbose) printDate(Mytime);

secs = Mytime.getSeconds();
if (verbose) cout `secs = $secs\n`;

dateString = Mytime.getDefaultString();
if (verbose) cout `asctime = $dateString`;

ctime(secs, dateString2.buffer);
if (verbose) cout `ctime = $dateString2`;

if (dateString!=dateString2)
   cout `Date format mismatch\n`;


Mytime2:=Date(secs);
if (Mytime2.getSeconds() != secs)
   cout `Date seconds conversion mismatch\n`;

Mytime = Date(); // Reset
Mytime.setUTCSeconds(0);
secs = Mytime.getSeconds();

if (verbose) cout `LocalSeconds = $secs\n`;

dateString2 = Mytime.strftime("%A %B %d %Y");
len = dateString2.size;

if (dateString2 != 'Thursday January 01 1970')
   cout `Date.strftime format mismatch, got '$dateString2', len = $len\n`;

cout `ok\n`;
