# Copyright 2010 Google Inc.

import crack.io StringFormatter, cout;
import crack.compiler CrackContext;
import crack.container TreeMap;

## increment everything but a constant zero.
void incrButZero(CrackContext ctx) {
    tok := ctx.getToken();
    if (tok.hasText('0'.buffer)) {
        ctx.putBack(tok);
    } else {
        ctx.inject('+ 1 '.buffer);
        ctx.putBack(tok);
    }
}

void testIsChecks(CrackContext ctx) {
    tok := ctx.getToken();
    if (tok.isIf())
        ctx.inject('1 '.buffer);
    else if (tok.isIdent())
        ctx.inject('2 '.buffer);
}

void emitOk(CrackContext ctx) {
    ctx.inject('cout `ok\n`;'.buffer);
}

TreeMap scopes;

class IntWrapper {
    int val;
    oper init(int val0) : val = val0 {}
}

void storeScope(CrackContext ctx) {
    scopes[String(ctx.getToken().getText())] = IntWrapper(ctx.getScope());
}

void getScope(CrackContext ctx) {
    v := IntWrapper.cast(scopes[String(ctx.getToken().getText())]).val;
    StringFormatter fmt;
    fmt `$v \0`;
    ctx.inject(fmt.createString().buffer);
}

void createdAnnotation(CrackContext ctx) {
    ctx.inject('true '.buffer);
}

void createdWithCallback(CrackContext ctx) {
    StringFormatter fmt;
    arg := IntWrapper.unsafeCast(ctx.getUserData());
    fmt `$(arg.val) \0`;
    ctx.inject(fmt.createString().buffer);
}

void createAnnotations(CrackContext ctx) {
    ctx.storeAnnotation('created'.buffer, createdAnnotation);
    ctx.storeAnnotation('createdWithCallback'.buffer, 
                        createdWithCallback,
                        IntWrapper(100)
                        );
}
