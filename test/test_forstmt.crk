# Copyright 2010-2011 Google Inc.
# 
#   This Source Code Form is subject to the terms of the Mozilla Public
#   License, v. 2.0. If a copy of the MPL was not distributed with this
#   file, You can obtain one at http://mozilla.org/MPL/2.0/.
# 
# tests of the "for" statement

int sum;

for (int i = 0; i < 10; ++i)
    sum += i;
if (sum != 45)
    puts('Failed on normal "for"');
sum = 0;

for (int i = 0; i < 10; ++i) {
    if (i % 2)
        continue;
    sum += i;
}
if (sum != 20)
    puts('Failed on continuing "for"');

for (;;) {
    sum = 100;
    break;
}
if (sum != 100)
    puts('Failed on empty "for"');

class RangeIter {
    int i, max;
    
    oper init(int start, int max) : i = start, max = max {}

    int elem() { return i; }
    
    bool next() { ++i; return i < max; }
    
    oper to bool() { return i < max; }
}

class Range {
    int start, max;
    oper init(int start, int max) : start = start, max = max {}
    RangeIter iter() { return RangeIter(start, max); }
}   

sum = 0;
for (i :in Range(0, 10))
    sum += i;
if (sum != 45)
    puts('Failed iteration with element var def');

if (1) {
    sum = 0;
    int i;
    for (i in Range(0, 10))
        sum += i;
    if (sum != 45)
        puts('Failed iteration over existing variable');
}

sum = 0;
for (i :on Range(0, 10))
    sum += i.elem();
if (sum != 45)
    puts('Failed iteration with iterator var def');

if (1) {
    sum = 0;
    RangeIter i;
    for (i on Range(0, 10))
        sum += i.elem();
    if (sum != 45)
        puts('Failed iteration with existing iterator');
}

puts('ok');
