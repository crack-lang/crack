// Copyright 2012 Google Inc.
// Copyright 2012 Conrad Steenberg <conrad.steenberg@gmail.com>
// 
//   This Source Code Form is subject to the terms of the Mozilla Public
//   License, v. 2.0. If a copy of the MPL was not distributed with this
//   file, You can obtain one at http://mozilla.org/MPL/2.0/.
// 
import crack.io cout, FStr, StringFormatter;
import crack.strutil StringArray;
import crack.cont.array Array;
import crack.cont.hashmap HashMap;
import crack.enc.json JSONParser, JSONStringFormatter;
import crack.math NAN, INFINITY, FP_NORMAL, FP_ZERO, FP_INFINITE, FP_NAN;

JSONParser parser = {};

StringArray testStrings = [r'"\/\n\r\b\t\\\"\017"', 'true', 'false', 'null',
                           '{"one":1}', '{"two":"two"}', '{"three":1.0}',
                           '[1,2,3]', '[1, "2", 3e0, {}]', '
{
  "Material": "rna",
  "Temperature": 37.0,
  "Sodium": 1.0,
  "AdditionalComplexes": [

  ],
  "MaxComplexSize": 1,
  "Strands": {
    "S": {
      "ConcScale": "uM",
      "Conc": 1.0,
      "Sequence": "CGAUGCAUGC",
      "Pos": 0
    }
  },
  "Pseudoknots": "false",
  "MinConcScale": "uM",
  "Magnesium": 0.0,
  "Dangles": "some",
  "MaxHistograms": 10,
  "MinConc": 0.0,
  "Strands order": [
    "S"
  ],
  "MaxConcFrac": 0.01
}'];

StringArray testResults =  [r'"/\n\r\b\t\"\017"', 'true', 'false', 'null',
                            '{"one": 1}', '{"two": "two"}', '{"three": 1.000000}',
                           '[1, 2, 3]', '[1, "2", 3.000000, {}]',

                            I'{"Material": "rna", "Temperature": 37.000000, \
                               "Sodium": 1.000000, "AdditionalComplexes": [], \
                               "MaxComplexSize": 1, "Strands": {"S": \
                               {"ConcScale": "uM", "Conc": 1.000000, \
                               "Sequence": "CGAUGCAUGC", "Pos": 0}}, \
                               "Pseudoknots": "false", "MinConcScale": "uM", \
                               "Magnesium": 0.000000, "Dangles": "some", \
                               "MaxHistograms": 10, "MinConc": 0.000000, \
                               "Strands order": ["S"], "MaxConcFrac": 0.010000}',

                            ];
Array[uint] failed = {};
StringArray failedResults = {};
uint i = 0;

for (input :in testStrings){
    parseVal := parser.parse(input);

    result := FStr() `$parseVal`;
    if (result != testResults[i]){
        failed.append(i);
        failedResults.append(result);
    }
    i++;
}

i = 0;
if (failed.count() > 0){
    cout `Tests failed:\n`;
    for (fi :in failed){
        cout `$(testStrings[fi]) ->\n$(failedResults[i]), expected\n$(testResults[fi])\n`;
        i++;
    }
}

// Test encoding of regular Crack structures
uints := Array[uint]![1,2,3,4,5,6,7];
ints := Array[int]![-1, -2, -3, -4, 5, 6, 7];
floats := Array[float64]![0, 1, -NAN, NAN, INFINITY, -INFINITY];

StringArray nativeExpected = ['[1, 2, 3, 4, 5, 6, 7]',
                            '[-1, -2, -3, -4, 5, 6, 7]',
                            '[0.000000, 1.000000, NaN, NaN, Infinity, -Infinity]'];

StringArray encodeResults = {};

JSONStringFormatter jfmt = {};
jfmt.format(uints);
encodeResults.append(jfmt.string());
jfmt.format(ints);
encodeResults.append(jfmt.string());
jfmt.format(floats);

i = 0;
for (encoded :in encodeResults){
    parseVal := parser.parse(encoded);
    result := FStr() `$parseVal`;
    if (result != nativeExpected[i])
        cout `$encoded -> $result, expected\n$(nativeExpected[i])\n`;
    i++;
}

HashMap[int, String] h = {};
h[0] = "one";
h[1] = "two";
h[2] = "three";

// Check that formatter doesn't recurse
JSONStringFormatter jfmt2 = {};
jfmt2.format(h);

cout `ok\n`;
